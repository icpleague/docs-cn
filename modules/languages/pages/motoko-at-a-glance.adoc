= {proglang} 一览
:关键词: Motoko,编程语言,互联网计算机,开发人员
:编程语言: Motoko
:company-id: DFINITY
ifdef::env-github,env-browser[:outfilesuffix:.adoc]
:IC: 互联网计算机

本摘要提供了对 {proglang} 的简单而全面的概述，并提供了一些关键特性示例，可帮助您识别您在其他语言中可能知道的操作和模式，以及它们在 {proglang} 中的样子。

== {proglang} 动机和目标

{company-id} 和 {platform} 的一种简单、有用的语言。

* 熟悉的语法
* 默认安全
* 使用 *canister* 模型结合智能合约
* 提供 {company-id} 和 {platform} 功能的无缝集成
* 充分利用现在和未来的 WebAssembly

## 关键设计点

{proglang} 从多种编程语言中汲取灵感，包括 Java、JavaScript、C#、Swift、Pony、ML、Haskell。

* 面向对象、函数式和命令式
* 对象作为成员的记录
* `async`/`await` 用于异步消息的顺序编程
* 具有简单泛型和子类型的结构类型
*安全算术（无界和检查）
* 默认情况下不可为空的类型
* 类似 JavaScript 的语法，但静态类型和理智

== 语义

* 按值调用（如 Java、C、JS 和 ML；不像 Haskell 和 Nix）
* 声明是局部相互递归的
* 参数化，有界多态性
* 子类型作为包含，而不是强制
*没有动态演员表
* 没有继承

== 实现

* 在 OCaml 中实现（利用 `wasm` 库）
* 简单的参考解释器
* WebAssembly 不太简单的编译器
   * 多通道，每通道输入 IR
   * 统一表示，未装箱算术
   * 两个空格 gc，消息之间的 gc
* 擦除多态性

== 语言特点

接下来的部分以简化的形式重点介绍 {proglang} 编程语言功能。
有关使用这些和其他功能的更多信息，请参阅link:../language-guide/language-manual{outfilesuffix}[Language quick reference] in the ink:../language-guide/motoko{outfilesuffix}[_Motoko编程语言指南_]。

=== 表达式

*标识符，例如`x`，`foo_bar`，`test'`，`List`，`Map`

* 用于分组的括号

* 类型注释以帮助类型推断，例如 `(42 : Int)`

=== 块和声明

* 每个声明后都需要分号

* 相互递归

* 明确标记的可变变量

```
type Delta = Nat;
func print() {
  Debug.print(Int.toText(counter));
};
let d : Delta = 42;
var counter = 1;
counter := counter + tmp;
print();
```

=== 控制流

if and if - else

```
if (b) …
if (b) … else …
```

switch and case

```
switch x { case (pat1) e1; …; case _ en }
```

while and loop

```
while (b) …

loop …
loop … while (b)`
```
for

```
for (pat in e) …
```

////
=== 标签，中断并继续

* `label l exp`
* `break l` (more generally, `break l exp`)
* `continue l`

标签确保控制流是结构化的 (no gotos)
////

== 原始类型

接下来的部分重点介绍 {proglang} 编程语言中的原始类型。

=== 无界整数

`Int`

* 默认情况下推断为负文字

* 文字： `13`, `0xf4`, `-20`, `+1`, `1_000_000`

=== 无限自然数

`Nat`

* 非负数，下溢陷阱

* 默认为非负文字推断

* 文字：`13`、`0xf4`、`1_000_000`

=== 有界数（陷阱）

`Nat8`, `Nat16`, `Nat32`, `Nat64`, `Int8`, `Int16`, `Int32`, `Int64`

* 上溢和下溢陷阱

* 需要指定类型注释

* 文字： `13`, `0xf4`, `-20`, `1_000_000`

=== 浮点数字

`Float`

* IEEE 754 双精度（64 位）语义，归一化 NaN

* 推断小数字面量

* 文字： `0`, `-10`, `2.71`, `-0.3e+15`, `3.141_592_653_589_793_12`

=== 数值运算

操作符合你的预期（没有意外）。

```
a - b  
a + b
a & b
```

===字符和文本

`字符', `文本'

Unicode，无随机访问

```
'x', '\u{\6a}', '☃'
"boo", "foo \u{\62}ar ☃"
"Concat" # "enation"
```

=== 布尔值

`Bool`

文字: `true`, `false`

```
a or b
a and b
not b  
if (b) e1 else e2
```

== 函数

接下来的部分提供了在 {proglang} 编程语言中使用函数的示例。

=== 函数类型

简单函数

```
Int.toText : Int -> Text
```

多个参数和返回值

```
divRem : (Int, Int) -> (Int, Int)
```

可以是通用的/多态  

```
Option.unwrapOr : <T>(?T, default : T) -> T
```

first-class (can be passed around, stored)  

```
map : <A, B>(f : A -> B, xs : [A]) -> [B]
let funcs : [<T>(T) -> T] = …
```

=== 函数声明和使用

`func() { … }` short for `func() : () = { … }`

参数函数

类型实例化有时会被省略

匿名函数 (a.k.a. lambdas)

```
func add(x : Int, y : Int) : Int = x + y;
```

```
func applyNTimes<T>(n : Nat, x : T, f : T -> ()) {
  if (n == 0) return;
  f(x);
  applyNTimes(n-1, x, f);
}
```

```
applyNTimes<Text>(10, "Hello!", func(x) = { Debug.print(x) } );
```

== 复合类型

接下来的章节提供了在{proglang}编程语言中使用复合类型的例子。

==== 元组

`(Bool, Float, Text)`

不可变、异构、固定大小

```
let tuple = (true, 1.2, "foo");
tuple.1 > 0.0;
let (_,_,t) = tuple;
```

=== 选项

`?Text`

要么是该类型的值，要么是“null”

```
func foo(x : ?Text) : Text {
  switch x {
    case (null) { "No value" };
    case (?y) { "Value: " # y };
  };
};
foo(null);
foo(?"Test");
```

=== 数组（不可变）

`[Text]`

```
let days = ["Monday", "Tuesday", … ];
assert(days.len() == 7);
assert(days[1] == "Tuesday");
// days[7] will trap (fixed size)
for (d in days.vals()) { Debug.print(d) };
```

=== 数组（可变）

`[var Nat]`

```
let counters = [var 1, 2, 3];
assert(counters.len() == 3);
counters[1] := counters[1] + 1;
// counters[3] will trap (fixed size)
```

=== 记录

`{name : Text; points : var Int}`

```
let player = { name = "Joachim";  var points = 0 };
Debug.print(
  player.name # " has " #
  Int.toText(player.points) # " points."
);
player.points += 1;
```

=== 对象

`{ get : () -> Int; add : Int -> () }`

不同的语法，与记录相同的类型

```
object self {
  var points = 0; // private by default
  public func get() = points;
  public func add(p : Int) { points += p };
}
```

=== 变体

`{ #invincible; #alive : Int; #dead }`

类似于枚举类型

```
type Health = { #invincible; #alive : Nat; #dead };
func takeDamage(h : Health, p : Nat) : Health {
  switch (h) {
    case (#invincible) #invincible;
    case (#alive hp) {
      if (hp > p) (#alive (hp-p)) else #dead
    };
    case (#dead) #dead;
  }
}
```

== 包和模块

接下来的部分提供了使用 {proglang} 编程语言处理包和模块的示例。

=== 模块

* 类型和值，如对象

* 仅限于 _static_ 内容（纯，无状态，...）

```
// the type of base/Int.mo
module {
  toText : Int -> Text;
  abs : Int -> Nat;
  …
}
```

=== 模块导入

* `base` 包提供基本功能

* 在社区支持下不断发展的其他库

```
import Debug "mo:base/Debug";
import Int "mo:base/Int";
```

== 平台功能

接下来的部分提供了 {proglang} 编程语言平台特定功能的示例。

=== Actor 类型

* 类似对象类型，但标记为 `actor`：

* _shareable_ 参数和 _no_ 或 _async_ 结果类型。

* “canister” ≈ “actor”

```
type Receiver = actor { recv : Text -> async Nat };
type Broadcast = actor {
  register : Receiver -> ();
  send : Text -> async Nat;
}
```

=== 可共享 ≈ 可序列化

* 所有原始类型

* 记录、元组、数组、变体、具有不可变可共享组件的选项

* `actor` 类型

* `shared` 函数类型

以下是**不可共享的：**

* 可变的东西

* 局部函数

* 对象（带有方法）

=== 完整的actor示例

典型的容器主文件
```
import Array "mo:base/Array";
actor {
  var r : [Receiver] = [];
  public func register(a : Receiver) {
    r := Array.append(r, [a]);
  };
  public func send(t : Text) : async Nat {
    var sum := 0;
    for (a in r.values()) {
      sum += await a.recv(t);
    };
    return sum;
  };
}
```

=== 异步/等待

`异步T`

* 异步未来或承诺

* 由 `async { ... }` 引入（隐含在异步函数声明中）

* `await e` 暂停计算等待 `e` 的结果

=== Actor导入

```
import Broadcast "ic:ABCDEF23";
actor Self {
  public func go() {
    Broadcast.register(Self);
  };
  public func recv(msg : Text) : async Nat {
    …
  }
}
```

=== 主体和调用

`Principal` 类型表示用户或容器/actor的身份

```
actor Self {
  let myself : Principal = Principal.fromActor(Self);
  public shared(context) func hello() : async Text {
    if (context.caller == myself) {
      "Talking to yourself is the first sign of madness";
    } else {
      "Hello, nice to see you";
    };
  };
}
```

== 类型系统

下一节重点介绍 {proglang} 编程语言中使用的类型系统的详细信息。

=== 结构

类型定义不创建类型，而是命名现有类型

```
type Health1 = { #invincible; #alive : Nat; #dead };
type Health2 = { #invincible; #alive : Nat; #dead };

let takeDamage : (Health1, Nat) -> Health1 = …;
let h : Health2 = #invincible;
let h' = takeDamage(h, 100); // works
```

=== 子类型

`Mortal <: Health`

```
type Health = { #invincible; #alive : Nat; #dead };
type Mortal = { #alive : Nat; #dead };

let takeDamage : (Health, Nat) -> Health = …;
let h : Mortal = #alive 1000;
let h' = takeDamage(h, 100); // also works
```

`t1 <: t2`：`t1` 可以在任何需要 `t2` 的地方使用

=== 通用类型

```
type List<T> = ?{head : T; tail : List<T>};

let l : List<Nat> = ?{head = 0; tail = ?{head = 1 ; tail = null }};
```

== 错误处理

```
try … catch …

throw …
```


== Language comparison cheat sheet

=== Literals

[width="100%",options="header",frame="topbot"]
|===
|Motoko |Ocaml |JavaScript/TypeScript

a|
----
> 3;
3 : Nat
----

a|
----
# 3;;
- : int = 3
----

a|
----
> 3
3
----

a|
----
> 3.141;
3.141 : Float
----

a|
----
# 3.141;;
- : float = 3.141
----

a|
----
> 3.141
3.141
----

a|
----
> “Hello world”;
“Hello world” : Text
----

a|
----
# “Hello world”;;
- : string = “Hello world”
----

a|
----
> “Hello world”
“Hello world”
----

a|
----
> ‘J’;
‘J’ : Char
----

a|
----
# ‘J’;;
- : char = ‘J’
----

a|
Does not have `char` literals -- use string

a|
----
> true;
true : Bool
----

a|
----
# true;;
- : bool = true
----

a|
----
> true
true
----

a|
----
> ();
----

a|
----
# ();;
- : unit = ()
----

a|
----
> undefined
----

a|
----
> (3, true, “hi”);
(3, true, “hi”) : (Nat, Bool, Text)
----

a|
----
# (3, true, “hi”);;
- : int * bool * string = 3, true, “hi”
----

a|
----
> [3, true, “hi”]
[3, true, “hi”]
----

a|
----
> [var 1, 2, 3];
[1, 2, 3] : [var Nat]
----

a|
----
# [\|1; 2; 3\|];;
- : int array = [\|1; 2; 3\|]
----

a|
----
> [1, 2, 3]
[1, 2, 3]
----

a|
----
> [1, 2, 3];
[1, 2, 3] : [Nat]
----

a|
----
# [1; 2; 3];;
- : int list = [1; 2; 3]
----

a|
----
> [1, 2, 3]
[1, 2, 3]
----
|===

=== Expressions

[width="100%",options="header"]
|===
|Motoko |Ocaml |JavaScript/TypeScript

a|
----
-3*(1+7)/2%3
----

a|
----
-3*(1+7)/2 mod 3
----

a|
----
-3*(1+7)/2%3
----

a|
----
-1.0 / 2.0 + 1.9 * x
----

a|
----
-1.0 /. 2.0 +. 1.9 *. x
----

a|
----
-1 / 2 + 1.9 * x
----
a|
----
a \|\| b && c
----

a|
----
a or b and c
----

a|
----
a \|\| b && c
----
|===

=== Functions

[width="100%",options="header"]
|===
|Motoko |Ocaml |JavaScript/TypeScript

a|
----
func<T1,T2,T3>(f : (T1, T2) -> T3) : T1 -> T2 -> T3 = func(x : T1) : T2 -> T3 = func(y : T2) : T3 = f(x,y)
----

a|
----
fun f -> fun x -> fun y -> f (x, y)
or
fun f x y -> f (x, y)
----

a|
----
f => x => y => f(x,y)
----

a|
----
func<T1, T2, T3>(f : (T1, T2) -> T3, x: T1, y : T2) : T3 = f (x,y)
----

a|
----
fun (f, x, y) -> f (x, y)
----

a|
----
([f, x, y]) => f(x,y)
----

a|
----
func f<T>(x:T) : T = x
----

a|
----
let f x = x
----

a|
----
f(x) { x }
----
a|
Does not have function pattern matching
----
func(x : Int) : Int =
  switch(x) {
    case (0) 0;
    case (n) 1;
  };
----

a|
----
function 0 -> 0
         \| n -> 1
----

a|
 
|===

=== Control flow

[width="100%",options="header"]
|===
|Motoko |Ocaml |JavaScript/TypeScript

a|
----
if (3 > 2) “X” else “Y”
----

a|
----
if 3 > 2 then “X” else “Y”
----

a|
----
if (3 > 2) { “X” } else { “Y” }
----

a|
----
import Debug “mo:base/Debug”;
if (3 > 2) Debug.print(“hello”);
----

a|
----
if 3 > 2 then print_string “hello”
----

a|
----
if (3 > 2) console.log(“hello”)
----

a|
----
while (true) {
  Debug.print(“X”);
}
----

a|
----
while true do
  print_string “X”
done
----

a|
----
while(true) {
  console.log(“X”);
}
----

a|
----
label L loop {
  if (x == 0) break L
  else continue L;
} while (true);
----

a|
没有 do while 循环——使用递归或 while

a|
----
do {
  if (x === 0) break;
  else continue;
} while (true);
----

a|
----
import Iter “mo:base/Iter”;
for (i in Iter.range(1,10)) {
  Debug.print(“X”);
};
----

a|
----
for i = 1 to 10 do
  print_string “X”
done
----

a|
----
for (i = 1; i <= 10; i++) {
  console.log(“X”);
}
----

a|
----
print_string “hello”;
print_string “world”
----

a|
----
print_string “hello”;
print_string “world”
----

a|
----
console.log(“hello”);
console.log(“world”);
----
|===

=== Value declarations

[width="100%",options="header"]
|===
|Motoko |Ocaml |JavaScript/TypeScript

a|
----
let name = expr;
----

a|
----
let name = expr
----

a|
----
const name = expr
----

a|
----
let f = func<T1, T2>(x : T1) : T2 { expr };
----

a|
----
let f x = expr
----

a|
----
const f = x => expr
----

a|
----
let fib = func(n : Nat) : Nat {expr};
----

a|
----
let rec fib n = expr
----

a|
----
const fib = n => expr
----
|===

=== Type declarations

[width="100%",options="header"]
|===
|Motoko |Ocaml |JavaScript/TypeScript

a|
----
type T = Int32 -> Bool
----

a|
----
type t = int -> bool
<int is 31-bit signed int>
----

a|
----
type t = (x: number) => boolean;
----

a|
----
type AssocList<K,V> = List<(K,V)>
----

a|
----
type (‘a, ‘b) assoc_list = (‘a * ‘b) list
----

a|
not applicable

|type option<T> = ?T
|type ‘a option = None | Some of ‘a
|type option<T> = T?

|type T = {#a : Int32; #b : U};
type U = (T, T);
|type t = A of int | B of u
and u = t * t
|not applicable

|type Complex = {#c : (Float, Float)};
func complex(x : Float, y : Float) : Complex = #c(x,y);
func coord(#c(x, y) : Complex) : (Float, Float) = (x, y);
|type complex = C of float * float
let complex (x,y) = C (x,y)
let coord (C (x,y)) = (x,y)
|not applicable
|===

=== Pattern matching

[width="100%",options="header"]
|===
|Motoko |Ocaml

a|
----
func get_opt<T>(opt : ?T, d : T) : T {
  switch(opt) {
    case (null) d;
    case (?x) x;
  };
}
----

a|
----
let get_opt (opt, d) =
  match opt with
    None -> d
  \| Some x -> x
----

a| 
Does not have guards -- use if
----
import prelude “mo:base/Prelude”;
func fac(x : Nat) : Nat {
  switch(x) {
    case (0) 1;
    case (n) if (n>0) n * fac(n-1) else Prelude.unreachable();
  };
}
----

a|
----
let rec fac = function
  0 -> 1
\| n when n>0 -> n * fac(n-1)
\| _ -> raise Hell
----

a|
Does not have as a pattern

a|
----
let foo ((x,y) as p) = (x,p,y)
----
|===

=== Tuples

[width="100%",options="header"]
|===
|Motoko |Ocaml |JavaScript/TypeScript

a|
----
type Foo = (Int32, Float, Text)
----

a|
----
type foo = int * float * string
----

a|
----
type foo = (number, number, string)
----

a|
----
let bar = (0, 3.14, “hi”)
----

a|
----
let bar = (0, 3.14, “hi”)
----

a|
----
const bar = [0, 3.14, “hi”]
----

a|
----
let x = bar.1
or
let (_, x, _) = bar
----

a|
----
let _, x, _ = bar in x
----

a|
----
const x = bar[1]
----
|===

=== Records

[width="100%",options="header"]
|===
|Motoko |Ocaml |JavaScript/TypeScript

a|
----
type foo = {x : Int32; y : Float; var s : Text}
----

a|
----
type foo = {x:int; y:float; mutable s:string}
----

a|
Everything is mutable
----
type foo = {
  x: number; y: number;
  s: string
}
----

a|
----
let bar = {x=0; y=3.14; var s=””}
----

a|
----
let bar = {x=0; y=3.14; s=””}
----

a|
----
const bar = {x:0; y:3.14; s:””}
----

a|
----
bar.x
bar.y
bar.s
----

a|
----
bar.x
bar.y
bar.s
----

a|
----
bar.x
bar.y
bar.s
----

a|
Does not do pattern matching on mutable fields
----
let {x=x; y=y} = bar
let {y=y} = bar
or
let {x;y} = bar
let {y} = bar
----

a|
----
let {x=x; y=y; s=s} = bar
let {y=y} = bar
or
let {x;y;s} = bar
let {y;_} = bar
----

a|
 
a|
----
bar.s := “something”
----

a|
----
bar.s <- “something”
----

a|
----
bar.s = “something”
----

a|
----
type Bar = { f: <T>T -> Int32 }
----

a|
----
type bar = { f:’a.’a->int }
----

a|
----
type bar = {
  f<T>(x:T): number;
}
----
|===

=== References and mutable variables

[width="100%",options="header"]
|===
|Motoko |Ocaml |JavaScript/TypeScript

a|
----
var r = 0;
----

a|
----
let r = ref 0
----

a|
----
let r = new Number(0) // object reference
or
let r = 0  // mutable variable
----

a|
----
r
----

a|
----
!r
or
r.contents
----

a|
----
r
----

a|
----
r := 1
----

a|
----
r := 1
or
r.contents <- 1
----

a|
----
r = 1
----

a|
Does not take mutable variables
----
func f(x : Nat) : Nat = x
----

a|
----
let f {contents=x} = x
----

a|

a|
----
r1 == r2
r1 != r2
----

a|
----
r1 == r2
r1 != r2
----

a|
----
r1.valueOf() === r2.valueOf()
r1 !== r2
----
|===

=== Comparison

[width="100%",options="header"]
|===
|Motoko |Ocaml |JavaScript/TypeScript

a|
----
2 == 2
2 != 3
----

a|
----
2 = 2
2 <> 3
----

a|
----
2 === 2
2 !== 3
----

a|
Does not have references
----
var r = 2;
var s = 2;
r == s
----

a|
----
let r = ref 2
r == r
r != ref 2
----

a|
 
a|
Does not have generic equality

a|
Does not have a generic equality
----
(2, r) != (2, r)
(2, r) = (2, ref 2)
----

a|
----
(2, r) === (2, r)
----
|===

=== Immutable and mutable arrays

[width="100%",options="header"]
|===
|Motoko |Ocaml

a|
----
import Array “mo:base/Array”;
Array.tabulate(20, func(x:Nat):Nat = x*x)
----

a|
----
List.init 20 (fun x -> x*x)
----

a|
----
Array.init(20, 1.0)
----

a|
----
Array.make 20 1.0
----

a|
----
a[2]
----

a|
----
Array.get a 2

a.(2)
----

a|
----
a[2] := x
----

a|
----
Array.set a 2 x
or
a.(2) <- x
----

a|
----
for (x in a.vals()) {
  Debug.print(x)
}
----
a|
----
List.iter print_string a
----
|===

=== Strings

[width="100%",options="header"]
|===
|Motoko |Ocaml

a|
----
“Hello ” # “world\n”
----
a|
----
“Hello “ ^ “world\n”
----

a|
----
Int.toText(13)
debug_show(3.141)
----

a|
----
string_of_int 13
string_of_float 3.141
----

a|
----
s.len()
----

a|
----
String.length s
----

a|
----
for (c in s.chars()) {
  Debug.print(debug_show(c))
}
----

a|
----
String.iter print_char s
----

a|
Does not have index access

a|
----
String.get s 0 or s.[0]
----
|===


== Class declaration example
以下表格用以对比在类宣告在proglang，JavaScript/TypeScript中的区别
The following table compares class declarations in {proglang} with class declarations in JavaScript and TypeScript.

[width="100%",options="header"]
|===
|Motoko |JavaScript/TypeScript

a|
----
class Counter(initValue:Nat) {
  var _value = initValue;
  public func get() : Nat {
    _value
  };
  func f(x: Nat) {};
}
----

a|
----
class Counter {
  private _value;
  constructor(initValue) { _value = initValue }
  public get() { return _value }
  private f(x) {}
}
----

a|
----
class Foo() = Self {
  func f() : Foo = Self
}
----

a| 
 
|===
